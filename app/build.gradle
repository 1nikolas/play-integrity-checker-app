plugins {
    id 'com.android.application'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "gr.nikolasspyr.integritycheck"
        minSdk 21
        targetSdk 32
        versionCode 02
        versionName "0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //Get variables from local.properties
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField "String", "API_URL", "\"${properties.getProperty('API_URL')}\""
    }

    buildFeatures {
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    tasks.whenTaskAdded { task ->
        switch (task.name) {
            case 'bundleRelease':
                task.finalizedBy copyMappings
                break
        }
    }

}

task copyMappingsToReleases(type: Copy) {
    from "${rootDir}/app/build/outputs/mapping/release/mapping.txt"
    into "${rootDir}/app/release"
}

task copyMappings(type: Copy, dependsOn: [copyMappingsToReleases]) {
    from "${rootDir}/app/build/outputs/mapping/release/mapping.txt"
    into "${rootDir}/app/mappings"
    rename { String fileName ->
        "mapping-${android.defaultConfig.versionName}-build-${android.defaultConfig.versionCode}.txt"
    }
}


dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation('com.google.android.play:integrity:1.0.1') {
        exclude group: 'com.android.support', module: 'support-compat'
    }

    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
}